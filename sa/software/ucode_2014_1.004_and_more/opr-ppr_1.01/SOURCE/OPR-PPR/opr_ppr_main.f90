
!!!!!! CHECK THE OPRPPR_CHECK PAR NAME COUNTER !!!!!!!!!!!!!!!!!!!!!!!!!

!
PROGRAM OPR_PPR
!
    USE OPR_PPR_VAR
    USE DATATYPES
    USE GLOBAL_DATA, ONLY : IVERB,MAX_STRING_LEN,LENDNAM
    USE DEPENDENTS, ONLY  : DEP_INI_READ,DEP_INI_ALLOC,DEP_INI_STORE,NDEPGPS
    USE BASIC, ONLY       : COVMATARR,BAS_INI_COVMAT
    USE UTILITIES, ONLY   : UTL_SAMENAME,UTL_STOP,UTL_DX_OPEN,UTL_DX_CLOSE,UTL_FILTER,UTL_READBLOCK, &
                            UTL_WRITEBLOCK,UTL_DX_READ_WT,UTL_WRITECDMATRIX,UTL_FILTERLIST,UTL_CASE, &
                            UTL_READMATRIX,UTL_COMBINESQMATRIX,UTL_CONSTRUCTCDMATRIX,UTL_GROUPLIST, &
                            UTL_GETARG,UTL_CALCWT,UTL_GETPOS,ASSIGNMENT(=)
    USE SENSITIVITY, ONLY : SEN_UEV_DX_READ_SU,SEN_UEV_DX_READ_MATRIX,SEN_UEV_DX_WRITE_MATRIX
    USE STATISTICS, ONLY  : STA_UEV_DX_READ_DM
    !
    IMPLICIT NONE
    !
    ! PARAMETERS AND VARIABLES
    INTEGER, ALLOCATABLE, DIMENSION(:)                 :: PLOTSYMBOLD             ! DUMMY TO STORE (UNUSED) PLOT SYMBOLS
    INTEGER, ALLOCATABLE, DIMENSION(:)                 :: IPTR
    INTEGER, ALLOCATABLE, DIMENSION(:)                 :: LN                      ! LOG-TRANSFORM FOR PARAMETERS
    INTEGER, ALLOCATABLE, DIMENSION(:,:)               :: PARIND                  ! PARIND=INDEX OF PARAMS IN EACH PPR ANALYSIS
    DOUBLE PRECISION, ALLOCATABLE, DIMENSION(:,:)      :: XOBS,XPRI               ! SENS OF EXISTING OBS AND PRIOR
    DOUBLE PRECISION, ALLOCATABLE, DIMENSION(:,:)      :: XSWP,XSWPLOC            ! SENS OF OBS AND PRIOR (WORKING)
    DOUBLE PRECISION, ALLOCATABLE, DIMENSION(:,:)      :: ZPRE                    ! SENS OF PREDICTIONS TO PARAMETERS
    DOUBLE PRECISION, ALLOCATABLE, DIMENSION(:,:)      :: XOBN                    ! SENS OF NEW OBSERVATIONS TO PARAMETERS
    DOUBLE PRECISION, ALLOCATABLE, DIMENSION(:)        :: PARVAL,CMDI             ! PARAM VALUE OR BSCAL : DIAGS OF (XTQX)
    DOUBLE PRECISION, ALLOCATABLE, DIMENSION(:,:)      :: PRIORWT                 ! PRIOR WEIGHT FOR PPR
    DOUBLE PRECISION, ALLOCATABLE, DIMENSION(:,:)      :: PVAR,PARV               ! PRED VARIANCES; PAR VARIANCES
    DOUBLE PRECISION, ALLOCATABLE, DIMENSION(:,:)      :: ZPARPREDRONLY           !
    REAL(KIND=4), ALLOCATABLE, DIMENSION(:,:,:)        :: PARCORR                 ! SINGLE PREC. CORRELATIONS FOR OPC/PPC
    REAL(KIND=4)                                       :: RTIME
    CHARACTER(LEN=MAX_STRING_LEN)                      :: BLOCKLAB,OUTNAM,WORD1,ARG ! FOR PROCESSING BLOCKS AND MISC TEXT
    CHARACTER(LEN=LENPNAM), ALLOCATABLE, DIMENSION(:)  :: PARNAM,PARNAMD,PNAMTEMP,PNAMTMP2 ! STORAGE FOR PARAMETER NAMES
    CHARACTER(LEN=LENPNAM)                             :: PARAMPAIR(2)            ! PARAMETER PAIR (NODE SENSITIVITIES _OPCNOD)
    CHARACTER(LEN=LENDNAM), ALLOCATABLE, DIMENSION(:)  :: PREDNAME,TNAME,OBSNAME,PRIORNAME,OBSNAMED,PREDNAMTMP1,TEMPNAME
    CHARACTER(LEN=LENDNAM)                             :: DUMNAM                  ! DUMMY
    CHARACTER(LEN=100)                                 :: CHECK
    CHARACTER(LEN=LENDNAM), ALLOCATABLE, DIMENSION(:,:):: OPRNAME                 ! OBS/PRIOR TO ADD/OMIT
    CHARACTER(LEN=LENDNAM), ALLOCATABLE, DIMENSION(:)  :: GRPNAME                 ! OBS/PRIOR GROUP NAMES (DEFAULT="GROUP//GRPNUM")
    CHARACTER(LEN=LENDNAM), ALLOCATABLE,  DIMENSION(:) :: OBSNAM,PREDNAM          ! OBS AND PRED AS READ FROM MAIN INPUT FILE
    CHARACTER(LEN=LENDNAM), ALLOCATABLE,  DIMENSION(:) :: PGRPNAME                ! PREDICTION GROUP NAMES
    INTEGER                         :: NUMGPS,IERR,TOTNEW,LENWORD,MAXOB,ITER,IVC,NNZ,NROW,NCOL,NLAY,NTIMES,NC,NR, &
                                       NUMNONZ,NG,ICALC,IDEPTYPE,NCOVMAT,IFAIL,KDEP,NDDEP,NEDEP,NTDEP,N,NUMPGPS,M,&
                                       NPERDTMP,IPOS,IC,IDUM,MAXNODECALCS,NUMITER,NPREDGPSUCODE,IUYDM,I,J,NNZF   !crt New: added NNZF. Is argument to subroutine UPDATEMATS
    INTEGER                         :: I_NODES,I_PREDDAT,I_PREDGRP,I_FILES,I_PARDAT,I_OMIT
    INTEGER(KIND=8)                 :: NFACT                         ! INTEGER FOR FACTORIAL FUNCTION
    DOUBLE PRECISION                :: RFACT                         ! DOUBLE FOR FACTORIAL FUNCTION
    TYPE (CDMATRIX)                 :: WTD,WTP,WTPP,WTM,WTMTMP,WTN,WTNSQR,WTMALL
    TYPE (LLIST), POINTER           :: TAIL,NODES_HEAD,FILES_HEAD,PARDAT_HEAD,OMIT_HEAD
    LOGICAL                         :: ALLOCATEERROR
    !
    ! FORMAT STATEMENTS
10  FORMAT(/,1X,'ENTER THE BASE NAME FOR THE MAIN INPUT/OUTPUT FILES: ',/)
20  FORMAT(/,1X,27('='),/,1X,A,/,1X,27('='),/)
25  FORMAT(/,1X,'PRECISION FOR REPORTING RESULTS: ',F, &
           /,1X,'(DETERMINED USING LAHEY FUNCION "EPSILON(X)")')
30  FORMAT(/,1X,'READING INPUT FROM FILE: ',A,/)
31  FORMAT(/,1X,'CAlIBRATION-PREDICTION FILE NAMES: ',                 &
           /,1x,'DMFNAM:    ',A                                        &
           /,1x,'SUFNAM:    ',A                                        &
           /,1x,'WTFNAM:    ',A                                        &
           /,1x,'SPUFNAM:   ',A                                        &
           /,1x,'DMPFNAM:   ',A                                        &
           /,1x,'SUPRIFNAM: ',A                                        &
           /,1x,'WTPRIFNAM: ',A)
32  FORMAT(/,1X,'PRIOR INFORMATION DATA FILE NAMES: ',                 &
           /,1x,'SUPRIFNAM: ',A                                        &
           /,1x,'WTPRIFNAM: ',A,/)
33  FORMAT(1X,'WTNFNAM:  ',A,': WEIGHTS FOR POTENTIAL OBS PROVIDED FROM _WT FILE',/)
34  FORMAT(/,1X,'POTENTIAL OBSERVATIONS ',A,' BE WEIGHTED USING USER FILE ',A,/)
35  FORMAT(/,1X,'MAKING BASE CASE Z(INV(XTWX)S)ZT CALCULATION',/)
36  FORMAT(/,1X,'PREDICTION MODEL INFORMATION FILE NAME (DMPFNAM): ',A,/)
37  FORMAT(/,1X,'NOTE: WEIGHTS FROM MAIN INPUT FILE ARE ECHOED ABOVE BUT ARE ', &
		 /,1X,'REPLACED BY ENTRIES IN FILE ',A,' FOR THE CALCULATIONS.',/)
40  FORMAT(/,1X,'COMPLETED BASE CASE Z(INV(XTWX)S)ZT CALCULATION',/)
42  FORMAT(/,1X,'OPTION BASECASE = YES - NO MORE CALCULATIONS TO BE MADE',/)
43  FORMAT(/,1X,'READING PREDICTION MODEL DATA FILE ',A,/)
44  FORMAT(2X,'PREDICTION DATA FILE ',A,' INDICATES ',I4,' PREDICTION-ONLY PARAMETER(S)',/)
45  FORMAT(/,1X,'NUMBER OF GROUPS DETERMINED FROM NPERD AND NParPerGroup: ',I8,/)
46  FORMAT(/,1X,'READING REGRESSION/SENSITIVITY MODEL DATA FILE ',A,/)
47  FORMAT(  1X,'  MODEL NAME: ',T16,A12, &
           /,1X,'       NPERD: ',T16,I5,  &
           /,1X,'    NPARPRED: ',T16,I5,  &
           /,1X,'       NDINC: ',T16,I5,  &
           /,1X,'         MPR: ',T16,I5,  &
           /,1X,'  TOTALPRIOR: ',T16,I5,  &
           /,1X,'         VAR: ',T16,1PD14.7, &
           /,1X,'(NOTE:   NPARPRED = NPERD + NPARPREDONLY)' &
           /,1X,'(      TOTALPRIOR =   MPR + NPARPREDONLY)',/)
48  FORMAT(/,1X,'  NOTE:   NUMBER ESTIMATED PARAMETERS: ',I4, &
           /,1X,'          TOTAL NUMBER OF PARAMETERS:  ',I4, &
           /,1X,'  IT IS RECOMMENDED THAT ALL PARAMETERS ARE INCLUDED',/)
49  FORMAT(/,1X,'  NOTE:   NUMBER OBSERVATIONS PROVIDED: ',I4, &
           /,1X,'          NUMBER OBSERVATIONS INCLUDED:  ',I4, &
           /,1X,'  IT IS RECOMMENDED THAT ALL OBSERVATIONS ARE INCLUDED',/)
50  FORMAT(/,1X,'PREDICTIONS AFTER INSERTING GROUP INFORMATION',/)
55  FORMAT(/,1X,'READING ',A,' FOR ',A,' FROM FILE: ',A,/)
58  FORMAT(/,1X,'SENSITIVITY ENTRIES FOR PRIOR WITH NPARPREDONLY APPENDED',/)
59  FORMAT(/,1X,'FULL WEIGHT MATRIX WITH ALL ENTRIES (OBSERVATIONS, EXISTING PRIOR AND NON-REGRESSION PRIOR)',/)
60  FORMAT(/,1X,'ALLOCATING SPACE FOR NODE-SENSITIVITIES: ',I8,A,/)
65  FORMAT(/,1X,'MAXNODECALCS = ',I4,': ONLY THE FIRST ',I4,' CALCULATIONS WILL BE MADE',/)
70  FORMAT(/,1X,'ADDED NODE-OBS ',A,' BE WEIGHTED USING USER FILE ',A,/)
75  FORMAT(3X,A,/,5X,'NUMBER OF ROWS    = ',I4, &
                /,5X,'NUMBER OF COLUMNS = ',I4, &
                /,5X,'NUMBER OF LAYERS  = ',I4, &
                /,5X,'NUMBER OF TIMES   = ',I4,/)
76  FORMAT(/,1X,'OPCNODOPTION = ',A,' - PARAMETER PAIR PROVIDED: ',A,1X,A,/)
77  FORMAT(/,1X,'OPCNODOPTION = ',A,' - MAXIMUM CORRELATION CHANGE FOR ALL PAIRS WILL BE RECORDED',/)
78  FORMAT(/,1X,'NUMBER OF POTENTIAL OBSERVATIONS = ',I6,/)
79  FORMAT(/,1X,'OMIT_DATA VALUES TO BE CONSIDERED ZERO SENSITIVITY: ',10F12.2,/)
80  FORMAT(/,1X,'NROW <> NCOL *OR* NROW <> TOTNEW IN WEIGHT FILE ',A,/)
81  FORMAT(/,1X,'OMIT_DATA BLOCK NOT FOUND: ALL NODE SENSITIVITIES WILL BE RETAINED',/)
83  FORMAT(/,1X,'OBSERVATION NAMES FOUND IN SAME SEQUENCE IN SENSITIVTY FILES ', &
		 /,1X,'(OBS_NAME_SEQUENCE = TRUE) - THIS ACCELERATES OPRPPR EXECUTION.')
84  FORMAT(/,1X,'OBSERVATION NAMES FOUND IN DIFFERENT SEQUENCE IN SENSITIVTY FILES ', &
		 /,1X,'(OBS_NAME_SEQUENCE = FALSE) - THIS CAN SLOW OPRPPR EXECUTION ', &
		 /,1X,'SINCE MANY NAME CHECKS MUST BE MADE - CONSIDER SORTING THE  ', &
		 /,1X,'OCCURENCE OF OBSERVATIONS TO BE THE SAME IN ALL FILES.')
85  FORMAT(/,1X,'TOTAL NUMBER OF PREDICTIONS IDENTIFIED FROM MAIN INPUT FILE =',I5, &
           /,1X,'(NOTE: THIS MUST EQUAL THE NO. OF ROWS IN THE PREDICTION SENSITIVITY FILE)',/)
90  FORMAT(1H+,'COMPLETING OPRADDNODE CALCULATION ',I5,' OF ',I5)
100 FORMAT(/,1X,'PPR CALCULATION FOR PARAMETER ',A,/)
110 FORMAT(/,1X,'PRIOR WEIGHT CALCULATION FOR PARAMETER ',A,' CONVERGED IN ',I4,' ITERATIONS',/)
120 FORMAT(/,1X,'PPR CALCULATION FOR GROUP ',I4,/)
    !
    ! INITIALIZE SOME VARIABLES
      NULLIFY(TAIL,NODES_HEAD,FILES_HEAD,PARDAT_HEAD,OMIT_HEAD) !swm: Nullify LLIST POINTERS
      RUNFLAG= 0              !DEFAULT PRIOR TO READING OPTIONS BLOCK
      PREDGROUPS = 'NO'       !DEFAULT
      OBSGROUPS = 'NO'        !DEFAULT
      GROUPS = 'NO'           !DEFAULT
      PARGROUPS = 'NO'        !DEFAULT
      BASECASE = 'NO'         !DEFAULT
      NODEWT = .FALSE.        !DEFAULT
      NPRED  = 1              !DEFAULT
      IVERB  = 3              !DEFAULT
      PERCENTREDUC=10.0D0     !DEFAULT
      CORRELTHRESH=0.90       !DEFAULT
      ICORRELTHRESH=0         !INITIAL
	  MAXNODECALCS=0          !INITIAL
  	  MAXCALCS=0              !INITIAL
      NPPERG = 1              !DEFAULT (EACH PARAM ASSESSED INDIVIDUALLY)
      NUMGPS = 1              !DEFAULT
      NROW   = 1              !DEFAULT
      NCOL   = 1              !DEFAULT
      NLAY   = 1              !DEFAULT
      NTIMES = 1              !DEFAULT
      NCOVMAT= 0
	  I_OMIT = 0
	  IDUM   = 0
      BLANK  = ' '
      ARG=BLANK
      AVPFNAM=BLANK
      DMFNAM =BLANK
	  DMPFNAM =BLANK
      SUFNAM =BLANK
      SUPRIFNAM =BLANK
      WTFNAM =BLANK
      WTPRIFNAM=BLANK
      SPUFNAM=BLANK
      SUNFNAM=BLANK
      WTNFNAM =BLANK
	  SUPRIPFNAM=BLANK
	  WTPRIPFNAM=BLANK
      PARFILE=BLANK
      GRIDSENSFILE =BLANK
      GRIDWTSFILE=BLANK
      CALIBRATIONPREDICTIONROOT=BLANK
	  OBS_NAME_SEQUENCE=.TRUE.
    !
    ! OPEN MAIN INPUT AND OUTPUT FILES
      ARG=UTL_GETARG(1)
      IF(ARG.EQ.' ')THEN
        WRITE(*,20) 'PROGRAM '//TRIM(PROGNAM)//' '//TRIM(VERSION)
        WRITE(*,10)
        READ(*,'(A)',ERR=999) AOPRIN
      ELSE
        AOPRIN=TRIM(ADJUSTL(ARG))
      END IF
      AOPROUT=TRIM(ADJUSTL(AOPRIN))//'.#OPR-PPR'
      AOPRIN=TRIM(ADJUSTL(AOPRIN))//'.IN'
      CALL OPRPPR_OPENFILE(IOUT,3,AOPROUT,6)
      WRITE(IOUT,20) 'PROGRAM '//TRIM(PROGNAM)//' '//TRIM(VERSION)
      CALL OPRPPR_OPENFILE(INUNIT,1,AOPRIN,IOUT)
      WRITE(IOUT,30) TRIM(ADJUSTL(AOPRIN))
    ! PRECISION
	  OPRPPR_PRECISION=EPSILON(OPRPPR_PRECISION)
	  WRITE(IOUT,25) OPRPPR_PRECISION
    !
    ! READ OPTIONS BLOCK AND DETERMINE THE TYPE OF RUN (OPTIONAL)
      CALL OPRPPR_READ_OPTIONS()
    !
    ! READ READ_FILES BLOCK (REQUIRED) CHECK FOR REQUIRED FILES
      I_FILES=0
      CALL UTL_READBLOCK(2,'READ_FILES',DUMCOL,INUNIT,IOUT,'*',.TRUE.,FILES_HEAD,TAIL,I_FILES)
      IF (IVERB.GE.3) CALL UTL_WRITEBLOCK(FILES_HEAD,IOUT)
      CALL UTL_FILTER(IERR,FILES_HEAD,IOUT,'CalibrationPredictionRoot',CalibrationPredictionRoot)
      IF(CALIBRATIONPREDICTIONROOT.NE.BLANK)THEN
        DMFNAM=TRIM(ADJUSTL(CALIBRATIONPREDICTIONROOT))//'._DM'
        SUFNAM=TRIM(ADJUSTL(CALIBRATIONPREDICTIONROOT))//'._SU'
        WTFNAM=TRIM(ADJUSTL(CALIBRATIONPREDICTIONROOT))//'._WT'
        SPUFNAM=TRIM(ADJUSTL(CALIBRATIONPREDICTIONROOT))//'._SPU'
	    DMPFNAM=TRIM(ADJUSTL(CALIBRATIONPREDICTIONROOT))//'._DMP'
	    SUPRIPFNAM=TRIM(ADJUSTL(CALIBRATIONPREDICTIONROOT))//'._SUPRIP'
	    WTPRIPFNAM=TRIM(ADJUSTL(CALIBRATIONPREDICTIONROOT))//'._WTPRIP'
        WRITE(IOUT,31)DMFNAM,SUFNAM,WTFNAM,SPUFNAM,DMPFNAM,SUPRIPFNAM,WTPRIPFNAM
      ELSE
        CALL UTL_FILTER(IERR,FILES_HEAD,IOUT,'DMFNAM',DMFNAM)
        IF (DMFNAM.EQ.BLANK) CALL UTL_STOP('DMFNAM = NULL - STOP (MAIN)')
	    CALL UTL_FILTER(IERR,FILES_HEAD,IOUT,'SUFNAM',SUFNAM)
        IF (SUFNAM.EQ.BLANK) CALL UTL_STOP('SUFNAM = NULL - STOP (MAIN)')
        CALL UTL_FILTER(IERR,FILES_HEAD,IOUT,'SUPRIFNAM',SUPRIFNAM)
        CALL UTL_FILTER(IERR,FILES_HEAD,IOUT,'WTFNAM',WTFNAM)
        IF (WTFNAM.EQ.BLANK) CALL UTL_STOP('WTFNAM = NULL - STOP (MAIN)')
        CALL UTL_FILTER(IERR,FILES_HEAD,IOUT,'WTPRIFNAM',WTPRIFNAM)
        CALL UTL_FILTER(IERR,FILES_HEAD,IOUT,'SPUFNAM',SPUFNAM)
        IF (SPUFNAM.EQ.BLANK) CALL UTL_STOP('SPUFNAM = NULL - STOP (MAIN)')
        IF(UTL_SAMENAME(WTFNAM,WTPRIFNAM)) CALL UTL_STOP('WTPRIFNAM = WTFNAM (MAIN)')
	    CALL UTL_FILTER(IERR,FILES_HEAD,IOUT,'DMPFNAM',DMPFNAM)
        IF (DMPFNAM.EQ.BLANK) CALL UTL_STOP('DMPFNAM = NULL - STOP (MAIN)')
	    CALL UTL_FILTER(IERR,FILES_HEAD,IOUT,'SUPRIPFNAM',SUPRIPFNAM)
	    CALL UTL_FILTER(IERR,FILES_HEAD,IOUT,'WTPRIPFNAM',WTPRIPFNAM)
      END IF
      IF (RUNFLAG.EQ.1.OR.RUNFLAG.EQ.2) THEN
        CALL UTL_FILTER(IERR,FILES_HEAD,IOUT,'SUNFNAM',SUNFNAM)
        IF (SUNFNAM.EQ.BLANK) CALL UTL_STOP('RUNMODE = OPRADD BUT SUNFNAM = NULL - STOP (MAIN)')
        CALL UTL_FILTER(IERR,FILES_HEAD,IOUT,'WTNFNAM',WTNFNAM)
        IF(WTNFNAM.EQ.BLANK) WRITE(IOUT,33)
        IF(UTL_SAMENAME(WTNFNAM,WTFNAM)) CALL UTL_STOP('WTNFNAM = WTFNAM (MAIN)')
        IF(UTL_SAMENAME(WTNFNAM,WTPRIFNAM).AND.WTNFNAM.NE.BLANK) CALL UTL_STOP('WTNFNAM = WTPRIFNAM (MAIN)')
      END IF
    !
    ! _DMP FILE CONTAINING BASIC MODEL INFORMATION FOR PREDICTION ONLY MODEL
      NPARPREDONLY=0
	  OUTNAM=DMPFNAM(1:LEN(TRIM(ADJUSTL(DMPFNAM)))-5)
	  IUYDM = UTL_DX_OPEN(OUTNAM,'_dmp','OLD')
	  IF(IUYDM.LE.0) STOP 'ERROR ENCOUNTERED OPENING ._DMP FILE - STOP (MAIN)'
      WRITE(IOUT,43) ADJUSTL(DMPFNAM)
      READ(IUYDM,*) CHECK,NPREDGPSUCODE
	  IF(CHECK.NE.'NUMBER OF PREDICTION GROUPS = ') CALL UTL_STOP('ERROR ENCOUNTERED READING _DMP FILE - STOP (MAIN)')
	  WRITE(IOUT,*) ' ',TRIM(ADJUSTL(CHECK)),NPREDGPSUCODE
	  READ(IUYDM,*) CHECK,NPERD
	  IF(CHECK.NE.'NUMBER OF PARAMETERS FOR PREDICTIVE EVALUATION = ') &
     	             CALL UTL_STOP('ERROR ENCOUNTERED READING _DMP FILE - STOP (MAIN)')
      WRITE(IOUT,*) ' ',TRIM(ADJUSTL(CHECK)),NPERD
      IUYDM=UTL_DX_CLOSE('_DMP','OLD')
    !
    ! _DM FILE CONTAINING BASIC MODEL INFORMATION FOR DIMENSIONING
      OUTNAM=DMFNAM(1:LEN(TRIM(ADJUSTL(DMFNAM)))-4)
      WRITE(IOUT,46) ADJUSTL(DMFNAM)
      CALL STA_UEV_DX_READ_DM(IFAIL,OUTNAM,AIC,BIC,DTLA,HQ,ICONVERGE,ITERP,KASHYAP,MLOFD,MLOFDP,MODELLENGTH, &
	                        MODELMASS,MODELNAME,MODELTIME,MPR,NDINC,NPE,NPERDTMP,NPS,NOBS,STAT1,STAT2,STDERR,VAR)
    !
    ! CALCULATE AND REPORT SOME DIMENSIONS INFO AND MAKE SOME CHECKS
      NPARPREDONLY=NPERD-NPERDTMP
	  WRITE(IOUT,44) TRIM(ADJUSTL(DMPFNAM)),NPARPREDONLY
      IF(IVERB.GE.3) WRITE(IOUT,47) MODELNAME,NPERDTMP,NPERD,NDINC,MPR,MPR+NPARPREDONLY,VAR
      IF(ICONVERGE.NE.'YES') WRITE(IOUT,*)('WARNING: _DM FILE INDICATES OPTIMIZATION DID NOT CONVERGE')
      IF(NPERD.EQ.0) CALL UTL_STOP('ERROR: NO PARAMETERS SPECIFIED - STOP (MAIN)')
      IF(NDINC.EQ.0) CALL UTL_STOP('ERROR: NO OBSERVATIONS SPECIFIED - STOP (MAIN)')
      IF(MPR.GT.0)THEN
        IF(CALIBRATIONPREDICTIONROOT.NE.BLANK)THEN
          WTPRIFNAM=TRIM(ADJUSTL(CALIBRATIONPREDICTIONROOT))//'._WTPRI'
          SUPRIFNAM=TRIM(ADJUSTL(CALIBRATIONPREDICTIONROOT))//'._SUPRI'
        END IF
        MSG=BLANK
        MSG='_DM FILE '//TRIM(ADJUSTL(DMFNAM))//' LISTS MPR>0 BUT SUPRIFNAM AND(OR) WTPRIFNAM = NULL (MAIN)'
        IF(WTPRIFNAM.EQ.BLANK.OR.SUPRIFNAM.EQ.BLANK) THEN
		  CALL UTL_STOP(TRIM(MSG))
        ELSE
          WRITE(IOUT,32)SUPRIFNAM,WTPRIFNAM
        END IF
      END IF
      IF(NPERDTMP.NE.NPE) WRITE(IOUT,48) NPERD,NPE
      IF(NDINC.NE.NOBS) THEN
        WRITE(IOUT,49) NOBS,NDINC
	    CALL UTL_STOP
	  END IF
    !
    ! IF OPRADD OR OPROMIT PROCESS OBS BLOCKS AND LOAD WORKING ARRAYS
      IF(RUNMODE.EQ.'OPRADD'.OR.RUNMODE.EQ.'OPROMIT') THEN
        IDEPTYPE = 1
        CALL DEP_INI_READ(IDEPTYPE,INUNIT,IOUT,NCOVMAT,KDEP,NDDEP,NEDEP,NTDEP)
	  IF(WTNFNAM.NE.BLANK) WRITE(IOUT,37) TRIM(WTNFNAM)
        CALL DEP_INI_ALLOC()
        ALLOCATE(OBSNAM(KDEP))
        !IF(RUNMODE.EQ.'OPRADD'.AND.NCOVMAT.GT.0) THEN
          CALL BAS_INI_COVMAT(INUNIT,IOUT,NCOVMAT)
        !  IF(IFAIL.GT.0) CALL UTL_STOP('PROBLEM ENCOUNTERED PROCESSING COVARIANCE MATRICES (MAIN)')
        !ENDIF
        CALL TYP_NULL(WTN)
        CALL TYP_NULL(WTNSQR)
        CALL DEP_INI_STORE(IDEPTYPE,IOUT,NCOVMAT,KDEP,COVMATARR,0,OBSNAM,DTLA,WTN,WTNSQR)
        IF (IVERB.GE.3.AND.RUNMODE.EQ.'OPRADD'.AND.WTFNAM.EQ.BLANK) &
                       CALL UTL_WRITECDMATRIX(WTN,0,IOUT,.FALSE.,OBSNAM,OBSNAM)
        NGC=NDEPGPS
        NGM=KDEP
        ALLOCATE(OPRNAME(NGC,NGM),GRPNAME(NGC),NUMOBS(NGC))
        IF(OBSGROUPS.EQ.'YES')THEN
          NUMOBS = 0
          CALL OPRPPR_LOADDATA(OPRNAME,GRPNAME,OBSNAM,KDEP,NUMGPS)
        ELSE
          NUMOBS = KDEP
          GRPNAME='DefaultObs'
          OPRNAME(1,1:KDEP) = OBSNAM
        END IF
      END IF
    !
    ! PROCESS PPR_Parameters BLOCK IF RUNMODE='PPR' AND PARGROUPS='NO'
      IF(RUNMODE.EQ.'PPR'.AND.PARGROUPS.EQ.'NO') THEN
        I_PARDAT=0
        CALL UTL_READBLOCK(1,'PPR_PARAMETERS',PARCOL,INUNIT,IOUT,'*',.TRUE.,PARDAT_HEAD,TAIL,I_PARDAT)
        IF (IVERB.GE.3) CALL UTL_WRITEBLOCK(PARDAT_HEAD,IOUT)
        NGC=1
        NGM=I_PARDAT
        ALLOCATE(OPRNAME(NGC,NGM),GRPNAME(NGC),NUMOBS(NGC),PNAMTEMP(NGM))
        NUMOBS(1)=I_PARDAT
        GRPNAME(1)='GROUP1       '
        CALL UTL_FILTERLIST(PARDAT_HEAD,IOUT,'PARNAME',I_PARDAT,IERR,PNAMTEMP,MORE)
        DO N=1,I_PARDAT
          OPRNAME(1,N)=PNAMTEMP(N)
        END DO
        DEALLOCATE(PNAMTEMP)
      END IF
    !
    ! ECHO WORK ARRAYS (DEBUG ONLY)
      IF(RUNFLAG.EQ.11.OR.ABS(RUNFLAG).EQ.2) THEN
        DO N=1,NUMGPS
          DO M=1,NUMOBS(N)
            WRITE(IOUT,'(2(1X,A12))')TRIM(OPRNAME(N,M)),TRIM(GRPNAME(N))
          END DO
        END DO
      END IF
    !
    ! PROCESS ADD_NODE_DATA BLOCK IF RUNMODE = OPRADDNODE
      IF (RUNFLAG.EQ.3) THEN
        NGC=1
        NGM=1
        ALLOCATE(NUMOBS(1),OPRNAME(NGC,NGM),GRPNAME(NGC))
        I_NODES=0
        CALL UTL_READBLOCK(2,'ADD_NODE_DATA',DUMCOL,INUNIT,IOUT,'*',.TRUE.,NODES_HEAD,TAIL,I_NODES)
        IF (IVERB.GE.3) CALL UTL_WRITEBLOCK(NODES_HEAD,IOUT)
        CALL UTL_FILTER(IERR,NODES_HEAD,IOUT,'MAXNODECALCS',MAXNODECALCS)
	  IF(MAXNODECALCS.GT.0) WRITE(IOUT,65) MAXNODECALCS,MAXNODECALCS
	  CALL UTL_FILTER(IERR,NODES_HEAD,IOUT,'GRIDSENSFILE',GRIDSENSFILE)
        IF (GRIDSENSFILE.EQ.BLANK) CALL UTL_STOP('ERROR: MODE = OPRADDNODE BUT GRIDSENSFILE = NULL - STOP (MAIN)')
        CALL UTL_FILTER(IERR,NODES_HEAD,IOUT,'GRIDWTSFILE',GRIDWTSFILE)
        IF (GRIDWTSFILE.NE.BLANK) THEN
          WRITE(IOUT,70) 'WILL',GRIDWTSFILE
          NODEWT=.TRUE.
        ELSE
          WRITE(IOUT,70) 'WILL NOT',' '   !SET WEIGHTS ALL EQUAL TO 1.0D0
        END IF
        CALL UTL_FILTER(IERR,NODES_HEAD,IOUT,'PARFILE',PARFILE)
        IF (PARFILE.EQ.BLANK) CALL UTL_STOP('MODE = OPRADDNODE BUT PARFILE = NULL - STOP (MAIN)')
        CALL UTL_FILTER(IERR,NODES_HEAD,IOUT,'NGRIDCOL',NCOL)
        CALL UTL_FILTER(IERR,NODES_HEAD,IOUT,'NGRIDROW',NROW)
        CALL UTL_FILTER(IERR,NODES_HEAD,IOUT,'NGRIDLAY',NLAY)
        CALL UTL_FILTER(IERR,NODES_HEAD,IOUT,'NTIMES',NTIMES)
        IF(NCOL.LE.0.OR.NROW.LE.0.OR.NLAY.LE.0.OR.NTIMES.LE.0.OR.IERR.NE.0) THEN
          WRITE(IOUT,75) 'ERROR IN GRID SENSITIVITY DIMENSIONS - STOP (MAIN)',NROW,NCOL,NLAY,NTIMES
          CALL UTL_STOP(' ')
        END IF
        NUMOBS(1)=NROW*NCOL*NLAY*NTIMES
        WRITE(IOUT,78) NUMOBS(1)
        CALL UTL_FILTER(IERR,NODES_HEAD,IOUT,'FILEFORMAT',WORD1)
        CALL UTL_CASE(WORD1,FILEFORMAT,1)
        IF(FILEFORMAT.NE.'BINARY'.AND.FILEFORMAT.NE.'ASCII'.AND.FILEFORMAT.NE.'TABLE') FILEFORMAT='BINARY'
        CALL UTL_FILTER(IERR,NODES_HEAD,IOUT,'OPCNODOPTION',WORD1)
        CALL UTL_CASE(WORD1,OPCNODOPTION,1)
        IF(OPCNODOPTION.NE.'NAMEDPAIR'.AND.OPCNODOPTION.NE.'ALLPAIRMAX') OPCNODOPTION='ALLPAIRMAX'
        IF(OPCNODOPTION.EQ.'NAMEDPAIR')THEN
          PARAMPAIR=BLANK
          WORD1=BLANK
          CALL UTL_FILTER(IERR,NODES_HEAD,IOUT,'PARAMPAIR1',WORD1)
          CALL UTL_CASE(WORD1,PARAMPAIR(1),1)
          CALL UTL_FILTER(IERR,NODES_HEAD,IOUT,'PARAMPAIR2',WORD1)
          CALL UTL_CASE(WORD1,PARAMPAIR(2),1)
          IF(PARAMPAIR(1).EQ.BLANK.OR.PARAMPAIR(2).EQ.BLANK) &
            CALL UTL_STOP('OPCNODOPTION = "NAMEDPAIR" - A PARAMETER PAIR MUST BE PROVIDED (MAIN)')
          IF(UTL_SAMENAME(PARAMPAIR(1),PARAMPAIR(2))) THEN
            MSG='PARAMETER PAIR PROVIDED LISTS IDENTICAL NAMES: '//TRIM(PARAMPAIR(1))//' (MAIN)'
            CALL UTL_STOP(TRIM(MSG))
          END IF
          WRITE(IOUT,76) TRIM(OPCNODOPTION),TRIM(PARAMPAIR(1)),TRIM(PARAMPAIR(2))
        ELSE
          WRITE(IOUT,77) TRIM(OPCNODOPTION)
        END IF
        ! CHECK FOR PRESENCE OF THE OMIT_DATA BLOCK
        CALL UTL_READBLOCK(2,'OMIT_DATA',DUMCOL,INUNIT,IOUT,'*',.FALSE.,OMIT_HEAD,TAIL,I_OMIT)
        IF (IVERB.GE.3) CALL UTL_WRITEBLOCK(OMIT_HEAD,IOUT)
	  IF(I_OMIT.GT.0) THEN
        	ALLOCATE(OMIT_DATA_VAL(I_OMIT))
          CALL UTL_FILTERLIST(OMIT_HEAD,IOUT,'VALUE',I_OMIT,IERR,OMIT_DATA_VAL,MORE)
		IF(IVERB.GE.3) WRITE(IOUT,79) OMIT_DATA_VAL
	  ELSE
		IF(IVERB.GE.3) WRITE(IOUT,81)
	  END IF
	END IF
    !
    ! PROCESS PREDICTION BLOCK(S): IF PREDGROUPS=NO STATS FOR ALL PREDICTIONS ARE REPORTED
      IF(PREDGROUPS.EQ.'YES') THEN
        I_PREDGRP=0
        CALL UTL_READBLOCK(2,'PREDICTION_GROUPS',PREDCOL,INUNIT,IOUT,'GROUPNAME',.TRUE.,PREDGRP_HEAD,TAIL,I_PREDGRP)
        ALLOCATE(PGRPNAME(I_PREDGRP),USEFLAGP(I_PREDGRP),NUMPRED(I_PREDGRP))
        PGRPNAME=BLANK
        CALL UTL_FILTERLIST(PREDGRP_HEAD,IOUT,'GROUPNAME',I_PREDGRP,IERR,PGRPNAME,MORE)
        IF (IERR.NE.0.OR.PGRPNAME(1).EQ.BLANK) CALL UTL_STOP('ERROR PROCESSING GROUPNAME IN PREDICTION_GROUPS (MAIN)')
        CALL UTL_FILTERLIST(PREDGRP_HEAD,IOUT,'USEFLAG',I_PREDGRP,IERR,USEFLAGP,MORE)
        NPGRPUSE=I_PREDGRP !FOR DUMMY NAME ARRAY IN OPRPPR_WRITE
        IF(NPGRPUSE.GT.MAXPGRP) CALL UTL_STOP('NPGRPUSE > MAXPGRP: INCREASE MAXPGRP AND RECOMPILE (MAIN)')
      ELSE
        I_PREDGRP=1
        NPGRPUSE=I_PREDGRP !FOR DUMMY NAME ARRAY IN OPRPPR_WRITE
        ALLOCATE(PGRPNAME(I_PREDGRP),USEFLAGP(I_PREDGRP),NUMPRED(I_PREDGRP))
        !USEFLAGP(1)=.TRUE.
      END IF
	USEFLAGP=.TRUE.
      I_PREDDAT=0
      CALL UTL_READBLOCK(2,'PREDICTION_DATA',PREDCOL,INUNIT,IOUT,'*',.TRUE.,PREDDAT_HEAD,TAIL,I_PREDDAT)
      ALLOCATE(PREDNAM(I_PREDDAT))
      NPRED=I_PREDDAT
      NUMPRED(1)=I_PREDDAT !DEFAULT (OVERWRITTEN IF PREDGROUPS.EQ.'YES')
      WRITE(IOUT,85) I_PREDDAT
      PREDNAM=BLANK
      CALL UTL_FILTERLIST(PREDDAT_HEAD,IOUT,'PREDNAME',I_PREDDAT,IERR,PREDNAM,MORE)
      IF (IERR.NE.0.OR.PREDNAM(1).EQ.BLANK) CALL UTL_STOP('ERROR PROCESSING PREDNAME IN PREDICTION_DATA (MAIN)')
      IF (PREDGROUPS.EQ.'YES') THEN
        CALL UTL_GROUPLIST('DefaultPred ',PREDGRP_HEAD,IOUT,PREDDAT_HEAD,I_PREDGRP,I_PREDDAT)
        CALL OPRPPR_LOADPRED(PGRPNAME,PREDNAM,I_PREDGRP,I_PREDDAT)
      ENDIF
      IF (IVERB.GE.3) WRITE(IOUT,50)
      IF (IVERB.GE.3) CALL UTL_WRITEBLOCK(PREDDAT_HEAD,IOUT)
    !
    ! IF RUNFLAG=12 DETERMINE NUMGPS AND NUM IN EACH GRP
      IF (RUNFLAG.EQ.12) THEN
        IF(NPERD.LE.20) NUMGPS=INT(NFACT(NPERD)/(NFACT(NPPERG)*(NFACT(NPERD-NPPERG))))
        IF(NPERD.GT.20) NUMGPS=INT(RFACT(NPERD)/(RFACT(NPPERG)*(RFACT(NPERD-NPPERG))))
        NGC=NUMGPS
        NGM=NPPERG
        ALLOCATE(OPRNAME(NGC,NGM),GRPNAME(NGC),NUMOBS(NGC))
        NUMOBS = NGM   !NUMOBS IS AN ARRAY
        WRITE(IOUT,45) NUMGPS
      END IF
    !
    ! DETERMINE MAX NO. OF ADDED OBS OR PRIOR IN ANY OPERATION FOR DIMENSIONING AND MANAGEMENT
      MAXNEW = 0
      IF (RUNFLAG.EQ.1.OR.RUNFLAG.EQ.3) MAXNEW=1
      IF (RUNFLAG.EQ.2)  MAXNEW=MAXVAL(NUMOBS)
      IF (RUNFLAG.EQ.11) MAXNEW=1
      IF (RUNFLAG.EQ.12) MAXNEW=NPPERG
      IF (RUNFLAG.GT.0.AND.MAXNEW.EQ.0) CALL UTL_STOP('RUNFLAG > O BUT NO NEW OBS OR PRIOR SPECIFIED - STOP (MAIN)')
      MAXOB = MAXNEW+NDINC+MPR+NPARPREDONLY

!	CALL CPU_TIME(RTIME)                             !TIME CHECKS
!      WRITE(*,*) '1. MATRIX ALLOCATIONS ',0,RTIME      !TIME CHECKS

    !
    ! ALLOCATE SOME ARRAYS
      ALLOCATE (IPTR(NPERD),OBSNAME(NDINC+MPR+NPARPREDONLY),OBSNAMED(NDINC),XOBS(NPERD,NDINC),PLOTSYMBOLD(NDINC),  &    !DEPENDENTS
                PARNAM(NPERD),PARNAMD(NPERD),PRIORNAME(MPR+NPARPREDONLY),XPRI(NPERD,MPR+NPARPREDONLY),LN(NPERD),   &    !PRIOR AND PARAM
                PREDNAME(NPRED),ZPRE(NPERD,NPRED),                                                                 &    !PREDICTIONS
                XSWP(NPERD,MAXOB),XSWPLOC(NPERD,MAXOB),PARVAL(NPERD),PRIORWT(3,NPERD),                             &    !WORKING
			  CMDI(NPERD),TEMPNAME(MAXOB),STAT=IERR)                                                                  !WORKING
      XOBS=0.0D0
      XPRI=0.0D0
      IF (ALLOCATEERROR(IOUT,IERR,'ALLOCATION: OBS ARRAYS')) CALL UTL_STOP('(MAIN)')
    !
    ! SENSITIVITIES FOR PREDICTIONS (THIS IS THE FIRST TIME WE STORE PARAMETER NAMES)
      PREDNAME=' '
      OUTNAM=SPUFNAM(1:LEN(TRIM(ADJUSTL(SPUFNAM)))-5)
      WRITE(IOUT,55) 'SENSITIVITIES','PREDICTIONS',TRIM(ADJUSTL(SPUFNAM))
      IDUM =UTL_DX_OPEN(OUTNAM,'_SPU','OLD')
      IF(IDUM.LE.0) STOP 'ERROR ENCOUNTERED OPENING ._SPU FILE - STOP (MAIN)'
      CALL SEN_UEV_DX_READ_MATRIX(IDUM,NPRED,NPERD,ZPRE,PARNAMD,PREDNAME)
      IDUM =UTL_DX_CLOSE('_SPU','OLD')
      IF (IVERB.GE.3) CALL OPRPPR_WRITEMATRIX(IOUT,NPRED,NPERD,PREDNAME,PARNAMD,ZPRE)
    !
    ! PROCESS PARAMETERS WITH NO REGRESSION SENSITIVITY
	IF(NPARPREDONLY.GT.0)THEN
	  ALLOCATE(PNAMTMP1(NPARPREDONLY),PREDONLYPRIOR_WT(NPARPREDONLY),PREDONLYPRIOR_STAT(NPARPREDONLY), &
                 PREDONLYPRIOR_STATFLAG(NPARPREDONLY),ZPARPREDRONLY(NPERD,NPARPREDONLY),PNAMTMP2(NPERD))
	  CALL TYP_NULL(WTPP)
	  IF(SUPRIPFNAM.NE.BLANK.AND.WTPRIPFNAM.NE.BLANK) THEN
		! SENSITIVITIES
		OUTNAM=SUPRIPFNAM(1:LEN(TRIM(ADJUSTL(SUPRIPFNAM)))-8)
          WRITE(IOUT,55) 'SENSITIVITIES','NPARPREDONLY PARAMETERS',TRIM(ADJUSTL(SUPRIPFNAM))
          INUNIT =UTL_DX_OPEN(OUTNAM,'_SUPRIP','OLD')
		IF(INUNIT.LE.0) STOP 'ERROR ENCOUNTERED OPENING ._SUPRIP FILE - STOP (MAIN)'
          CALL SEN_UEV_DX_READ_MATRIX(INUNIT,NPARPREDONLY,NPERD,ZPARPREDRONLY,PNAMTMP2,PNAMTMP1)
		CALL OPRPPR_CHECKPAR(IOUT,NPERD,PARNAMD,PNAMTMP2,SPUFNAM,SUPRIFNAM,NPERD)
          INUNIT =UTL_DX_CLOSE('_SUPRIP','OLD')
          IF (IVERB.GE.3) CALL OPRPPR_WRITEMATRIX(IOUT,NPARPREDONLY,NPERD,PNAMTMP1,PNAMTMP2,ZPARPREDRONLY)
          OUTNAM=WTPRIPFNAM(1:LEN(TRIM(ADJUSTL(WTPRIPFNAM)))-8)
          WRITE(IOUT,55) 'WEIGHTS ON PRIOR FOR','NPARPREDONLY PARAMETERS',TRIM(ADJUSTL(WTPRIPFNAM))
          CALL UTL_DX_READ_WT(IOUT,OUTNAM,'_wtprip',WTPP)
	  ELSE
	    ! CHECK FOR PREDONLY_PRIOR BLOCK (CURRENTLY CAN NOT DO BOTH)
          CALL UTL_READBLOCK(2,'PREDONLY_PRIOR',PRICOL,INUNIT,IOUT,'PARAMNAME',.FALSE.,PREDONLY_HEAD,TAIL,IDUM)
	    IF(IDUM.NE.NPARPREDONLY) CALL UTL_STOP('NPARPREDONLY .NE. IDUM - STOP (MAIN)')
          CALL UTL_FILTERLIST(PREDONLY_HEAD,IOUT,'PARAMNAME',NPARPREDONLY,IERR,PNAMTMP1,MORE)
          DO N=1,NPARPREDONLY
            IF(.NOT.UTL_SAMENAME(PARNAMD(N+NPERDTMP),PNAMTMP1(N))) &
              CALL UTL_STOP ('ERROR MATCHING PARAMETER LISTED WITH NON-REGRESSION PRIOR WITH _SPU FILE CONTENTS (MAIN)')
              OBSNAME(NDINC+N)(1:LEN(TRIM(PNAMTMP1(N))))=PNAMTMP1(N)(1:LEN(TRIM(PNAMTMP1(N))))
          END DO
          CALL UTL_FILTERLIST(PREDONLY_HEAD,IOUT,'STATFLAG',NPARPREDONLY,IERR,PREDONLYPRIOR_STATFLAG,MORE)
          CALL UTL_FILTERLIST(PREDONLY_HEAD,IOUT,'STATISTIC',NPARPREDONLY,IERR,PREDONLYPRIOR_STAT,MORE)
          DO N=1,NPARPREDONLY
            CALL UTL_CALCWT(IOUT,PNAMTMP1(N),PREDONLYPRIOR_STATFLAG(N),PREDONLYPRIOR_STAT(N), &
                            -999.0D0,1.0D0,IERR,PREDONLYPRIOR_WT(N),RDUMD)
          END DO
          CALL UTL_CONSTRUCTCDMATRIX(NPARPREDONLY,NPARPREDONLY,NPARPREDONLY,WTPP,'WTPP')
          NNZ = 0
          WTPP%NNZ = 0
          DO N=1,NPARPREDONLY
            IPOS=N*N
            NNZ=NNZ+1
            WTPP%IPOS(NNZ) = N*N
            WTPP%DVAL(NNZ) = PREDONLYPRIOR_WT(N)
            WTPP%ICOL(NNZ) = N
            IC = N
            WTPP%NNZ = NNZ
            WTPP%ICOL(IC) = NNZ
          END DO
	  END IF
	  !ECHO WEIGHT MATRIX
        IF(IVERB.EQ.3) CALL UTL_WRITECDMATRIX(WTPP,0,IOUT,.FALSE.,PNAMTMP1,PNAMTMP1)
        IF(IVERB.GE.5) CALL UTL_WRITECDMATRIX(WTPP,1,IOUT,.FALSE.,PNAMTMP1,PNAMTMP1)
	END IF
    !
    ! CLOSE MAIN INPUT FILE
      CALL OPRPPR_OPENFILE(INUNIT,2,AOPRIN,IOUT)
    !
    ! SENSITIVITIES FOR EXISTING OBSERVATIONS (THIS IS THE SECOND TIME WE STORE PARAMETER NAMES)
      PARNAM=' '
      OBSNAMED=' '
      OUTNAM=SUFNAM(1:LEN(TRIM(ADJUSTL(SUFNAM)))-4)
      WRITE(IOUT,55) 'SENSITIVITIES','EXISTING OBSERVATIONS',TRIM(ADJUSTL(SUFNAM))
      CALL SEN_UEV_DX_READ_SU(NDINC,NPERD-NPARPREDONLY,OBSNAMED,OUTNAM,PARNAM(1:NPERD-NPARPREDONLY), &
                              PLOTSYMBOLD,XOBS(1:NPERD-NPARPREDONLY,1:NDINC))
      IF (NPARPREDONLY.GT.0) THEN
	  DO N=1,NPARPREDONLY
	    IF(SUPRIPFNAM.NE.BLANK.AND.WTPRIPFNAM.NE.BLANK) THEN
	      PARNAM(NPERDTMP+N)=PNAMTMP2(NPERDTMP+N)
		ELSE
	      PARNAM(NPERDTMP+N)=PNAMTMP1(N)
		END IF
	  END DO
	END IF
      CALL OPRPPR_CHECKPAR(IOUT,NPERD,PARNAM,PARNAMD,SUFNAM,SPUFNAM,NPERD-NPARPREDONLY)
      IF (IVERB.GE.3) CALL OPRPPR_WRITEMATRIX(IOUT,NDINC,NPERD,OBSNAMED,PARNAM,XOBS)
    !
    ! SENSITIVITY ENTRIES FOR EXISTING PRIOR INFORMATION
      PRIORNAME=' '
      IF(MPR.GT.0) THEN
        OUTNAM=SUPRIFNAM(1:LEN(TRIM(ADJUSTL(SUPRIFNAM)))-7)
        WRITE(IOUT,55) 'SENSITIVITIES','EXISTING PRIOR INFO',TRIM(ADJUSTL(SUPRIFNAM))
        INUNIT =UTL_DX_OPEN(OUTNAM,'_SUPRI','OLD')
	    IF(INUNIT.LE.0) STOP 'ERROR ENCOUNTERED OPENING ._SUPRI FILE - STOP (MAIN)'
        CALL SEN_UEV_DX_READ_MATRIX(INUNIT,MPR,NPERD-NPARPREDONLY,XPRI(1:NPERD-NPARPREDONLY,1:MPR), &
                                    PARNAMD(1:NPERD-NPARPREDONLY),PRIORNAME(1:MPR))
        INUNIT =UTL_DX_CLOSE('_SUPRI','OLD')
        CALL OPRPPR_CHECKPAR(IOUT,NPERD,PARNAM,PARNAMD,SUFNAM,SUPRIFNAM,NPERD)
	  IF (IVERB.GE.3) CALL OPRPPR_WRITEMATRIX(IOUT,MPR,NPERD,PRIORNAME,PARNAM,XPRI)
      END IF
    !
    ! SENSITIVITY ENTRIES FOR PRIOR NOT IN THE REGRESSION
      IF (NPARPREDONLY.GT.0) THEN
        DO M=1,NPARPREDONLY
		PRIORNAME(MPR+M)=PNAMTMP1(M)
		IF (SUPRIPFNAM.EQ.BLANK.AND.WTPRIPFNAM.EQ.BLANK) THEN
            XPRI(NPERDTMP+M,MPR+M)=1.0D0
		ELSE
		  DO N=1,NPERD
              XPRI(N,MPR+M)=ZPARPREDRONLY(N,M)
		  END DO
		END IF
        END DO
        WRITE(IOUT,58)
        IF (IVERB.GE.3) CALL OPRPPR_WRITEMATRIX(IOUT,MPR+NPARPREDONLY,NPERD,PRIORNAME,PARNAM,XPRI)
      END IF
    !
    ! COMBINE EXISTING OBS AND PRIOR NAMES
      OBSNAME(1:NDINC)=OBSNAMED(1:NDINC)
      IF(MPR.GT.0) OBSNAME(NDINC+1:NDINC+MPR)=PRIORNAME(1:MPR)
      IF(NPARPREDONLY.GT.0) OBSNAME(NDINC+MPR+1:NDINC+MPR+NPARPREDONLY)=PRIORNAME(MPR+1:MPR+NPARPREDONLY)
    !
    ! WEIGHTS FOR EXISTING OBSERVATIONS AND PRIOR
      CALL TYP_NULL(WTMTMP)
      OUTNAM=WTFNAM(1:LEN(TRIM(ADJUSTL(WTFNAM)))-4)
      WRITE(IOUT,55) 'WEIGHTS','EXISTING OBSERVATIONS',TRIM(ADJUSTL(WTFNAM))
      IF(MPR.GT.0) THEN
        CALL TYP_NULL(WTD)
        CALL TYP_NULL(WTP)
        CALL UTL_DX_READ_WT(IOUT,OUTNAM,'_wt',WTD)
        OUTNAM=WTPRIFNAM(1:LEN(TRIM(ADJUSTL(WTPRIFNAM)))-7)
        WRITE(IOUT,55) 'WEIGHTS','EXISTING PRIOR INFO',TRIM(ADJUSTL(WTPRIFNAM))
        CALL UTL_DX_READ_WT(IOUT,OUTNAM,'_wtpri',WTP)
        CALL UTL_COMBINESQMATRIX(WTD,WTP,WTMTMP)
        CALL TYP_DEALLOC(WTD)
        CALL TYP_DEALLOC(WTP)
      ELSE
        CALL UTL_DX_READ_WT(IOUT,OUTNAM,'_wt',WTMTMP)
      END IF
      IF(NPARPREDONLY.GT.0) THEN
	  ! COMBINE THE WEIGHT MATRICES FROM OBSERVATIONS AND NPARPREDONLY
        CALL UTL_COMBINESQMATRIX(WTMTMP,WTPP,WTM)
      ELSE
        WTM=WTMTMP
      ENDIF
      WRITE(IOUT,59)
      IF(IVERB.EQ.3) CALL UTL_WRITECDMATRIX(WTM,0,IOUT,.FALSE.,OBSNAME,OBSNAME)
      IF(IVERB.GE.5) CALL UTL_WRITECDMATRIX(WTM,1,IOUT,.FALSE.,OBSNAME,OBSNAME)
    !
    ! GET SOME DIMENSIONS FOR ALLOCATING THE XOBN AND TNAME ARRAYS
      IF (RUNFLAG.GT.0.AND.RUNFLAG.NE.3) THEN
        TOTNEW=SUM(NUMOBS)
      ELSEIF (RUNFLAG.LE.0) THEN
        TOTNEW=0
      ELSEIF (RUNFLAG.EQ.3) THEN ! REPORT ARRAY SIZE
        TOTNEW=NUMOBS(1)
        IF(REAL((TOTNEW*NPERD*8)/1000).LT.1000.) WRITE(IOUT,60) INT(TOTNEW*NPERD*8/1000),' KB'
        IF(REAL((TOTNEW*NPERD*8)/1000).GE.1000.) WRITE(IOUT,60) INT(TOTNEW*NPERD*8/1.0E+6),' MB'
      END IF
      
!crt   CALL CPU_TIME(RTIME)                                 !TIME CHECKS
!crt   WRITE(*,*) '2. XOBN AND WT PROCESSING ',0,RTIME      !TIME CHECKS

    !
    ! IF MODE=OPRADD GET SENSITIVITIES AND WEIGHTS FOR POTENTIAL OBSERVATIONS
      IF (RUNFLAG.EQ.1.OR.RUNFLAG.EQ.2) THEN
        ! IF USER PROVIDED WT FILE FOR POTENTIAL OBS RESET THE WEIGHT MATRIX
        IF(WTNFNAM.NE.' ') THEN
          CALL TYP_NULL(WTN)
          OUTNAM=WTNFNAM(1:LEN(TRIM(ADJUSTL(WTNFNAM)))-4)
          WRITE(IOUT,55) 'WEIGHTS','POTENTIAL OBS',TRIM(ADJUSTL(WTNFNAM))
          CALL UTL_DX_READ_WT(IOUT,OUTNAM,'_wt',WTN)
          TOTNEW=WTN%NC
        END IF
        ALLOCATE (XOBN(NPERD,TOTNEW),TNAME(TOTNEW),STAT=IERR)
        IF (ALLOCATEERROR(IOUT,IERR,'ALLOCATION:  ARRAYS FOR POTENTIAL OBS')) CALL UTL_STOP('(MAIN)')
        XOBN=0.0D0
        WRITE(IOUT,55) 'SENSITIVITIES','POTENTIAL OBS',TRIM(ADJUSTL(SUNFNAM))
        OUTNAM=SUNFNAM(1:LEN(TRIM(ADJUSTL(SUNFNAM)))-4)
        INUNIT =UTL_DX_OPEN(OUTNAM,'_SU','OLD')
	    IF(INUNIT.LE.0) STOP 'ERROR ENCOUNTERED OPENING ._SU FILE - STOP (MAIN)'
        CALL SEN_UEV_DX_READ_MATRIX(INUNIT,TOTNEW,NPERD-NPARPREDONLY, &
                                    XOBN(1:NPERD-NPARPREDONLY,1:TOTNEW), &
                                    PARNAMD(1:NPERD-NPARPREDONLY),TNAME)
        INUNIT =UTL_DX_CLOSE('_SU','OLD')
        CALL OPRPPR_CHECKPAR(IOUT,NPERD,PARNAM,PARNAMD,SUFNAM,SUNFNAM,NPERD-NPARPREDONLY)
        ! ECHO THE MATRICES
        IF (IVERB.GE.3) CALL OPRPPR_WRITEMATRIX(IOUT,TOTNEW,NPERD,TNAME,PARNAM,XOBN)
        IF (IVERB.EQ.3) CALL UTL_WRITECDMATRIX(WTN,0,IOUT,.FALSE.,TNAME,TNAME)
        IF (IVERB.GE.5) CALL UTL_WRITECDMATRIX(WTN,1,IOUT,.FALSE.,TNAME,TNAME)
	    ! DETERMINE IF OBS_NAME_SEQUENCE=.TRUE. : SAVES LATER NAME CHECKS
	    DO N=1,TOTNEW
          IF(.NOT.UTL_SAMENAME(TNAME(N),OBSNAM(N))) THEN
		  OBS_NAME_SEQUENCE=.FALSE.
		  EXIT
		END IF
	  END DO
	  IF(OBS_NAME_SEQUENCE) THEN
          WRITE(IOUT,83)
        ELSE
	    WRITE(IOUT,84)
	  END IF
      ELSE
        ALLOCATE (XOBN(NPERD,TOTNEW),TNAME(TOTNEW),STAT=IERR)
        IF (ALLOCATEERROR(IOUT,IERR,'ALLOCATION: ARRAYS FOR POTENTIAL OBS')) CALL UTL_STOP('(MAIN)')
	  ! INITIALIZE
	    XOBN=0.0D0
      END IF
    !
    ! ALLOCATE MODEL TIME ARRAYS
      IF(RUNFLAG.NE.3) NTIMES = 1 !FOR DIMENSIONING
      ALLOCATE (KSTP(NTIMES),KPER(NTIMES),PERTIM(NTIMES),TOTIM(NTIMES),STAT=IERR)
      IF (ALLOCATEERROR(IOUT,IERR,'ALLOCATION: MODEL TIME ARRAYS')) CALL UTL_STOP('(MAIN)')
    !
    ! IF ADDING NODE SENSITIVITIES GET NODE SENSITIVITIES
      IF (RUNFLAG.EQ.3) THEN
        ! MF2K _B FILE FOR VALUES TO UNSCALE ONE-PERCENT SENSITIVITIES
        CALL OPRPPR_READB(PARVAL,PARNAM,LN)                             !ADD CODE TO READ UCODE PARAMETER FILE AS ALTERNATIVE?
        ! FILE OF NODE SENSITIVITIES
        CALL OPRPPR_READGSF(PARNAM,XOBN,NROW,NCOL,NLAY,NTIMES,TOTNEW,PARVAL,LN,TNAME,I_OMIT)
        ! READ OR ASSIGN WT FOR EACH NODE LOCATION
        IF(NODEWT)THEN
          OUTNAM=GRIDWTSFILE(1:LEN(TRIM(ADJUSTL(GRIDWTSFILE)))-4)
          WRITE(IOUT,55) 'MATRIX FILE','NODE WEIGHTS',TRIM(ADJUSTL(GridWtsFile))
          CALL UTL_DX_READ_WT(IOUT,OUTNAM,'_wt',WTN)
          NC=WTN%NC
          NR=WTN%NR
          IF((NR.NE.NC).OR.(NR.NE.TOTNEW)) THEN    ! CHECK DIMS CONSISTENT
            WRITE(IOUT,80) TRIM(ADJUSTL(GRIDWTSFILE))
            CALL UTL_STOP('(MAIN)')
          END IF
        END IF
        ! IF OPCNODOPTION=NAMEDPAIR GET THE CORRECT INDICES
        IF(OPCNODOPTION.EQ.'NAMEDPAIR')THEN
          IPARAMPAIR=0
          DO N=1,NPERD
            IF(UTL_SAMENAME(PARNAM(N),PARAMPAIR(1))) IPARAMPAIR(1)=N
            IF(UTL_SAMENAME(PARNAM(N),PARAMPAIR(2))) IPARAMPAIR(2)=N
          END DO
          IF(COUNT(IPARAMPAIR>0).NE.2) CALL UTL_STOP('ERROR MATCHING PARAMETER PAIR WITH PARAMETER LIST (MAIN)')
        END IF
	ENDIF
    !
    ! DETEMINE IVC, NO. OF CALLS TO OPRPPR_CALC IN ADDITION TO BASE CASE
    IVC=0
    IF(OBSGROUPS.EQ.'YES'.OR.PARGROUPS.EQ.'YES') GROUPS='YES'
    IF(ABS(NUMGPS).EQ.1.AND.GROUPS.EQ.'YES')     IVC=1                !IDEAL FOR PPR=11?
    IF(ABS(NUMGPS).EQ.1.AND.GROUPS.EQ.'NO')      IVC=NUMOBS(1)        !IDEAL FOR PPR=11?
    IF(ABS(NUMGPS).GT.1)                         IVC=NUMGPS           !IDEAL FOR PPR=11?
    IF(RUNFLAG.EQ.3)                             IVC=TOTNEW           !IDEAL FOR PPR=11?
    IF(IVC.EQ.0) CALL UTL_STOP('IVC=0: NO OPR OR PPR CALCULATIONS TO MAKE (MAIN)')
    !
    ! ALLOCATE ARRAYS TO STORE PREDICTION AND PARAMETER VARIANCES
    ALLOCATE(PVAR(NPRED,0:IVC),PARV(NPERD,0:IVC),PARIND(IVC,NPPERG),PARCORR(0:IVC,NPERD,NPERD),STAT=IERR)
    IF(ALLOCATEERROR(IOUT,IERR,'ALLOCATION: VARIANCE ARRAYS')) CALL UTL_STOP('(MAIN)')

!crt    CALL CPU_TIME(RTIME)                                 !TIME CHECKS
!crt    WRITE(*,*) '3. XOBN AND WT PROCESSING ',0,RTIME      !TIME CHECKS

    !
    ! IF RUNFLAG=12 IDENTIFY ALL POSSIBLE IPPERG-SIZE GRPS
    IF(RUNFLAG.EQ.12) CALL OPRPPR_PPRGRPS(NUMGPS,PARNAM,PARIND,IVC,OPRNAME,GRPNAME)
    !
    ! MAKE SOME NAME CHECKS TO ENSURE NO CONFLICTS
    IF(RUNFLAG.NE.3.AND.RUNFLAG.NE.12) CALL OPRPPR_NAMECHECK(NUMGPS,TNAME,TOTNEW,OBSNAME,PARNAM,IVC,PARIND,OPRNAME)

!crt	CALL CPU_TIME(RTIME)                              !TIME CHECKS
!crt    WRITE(*,*) '4. LOAD WORKING ARRAYS ',0,RTIME      !TIME CHECKS

    !
    !	LOAD UP THE WORKING MATRICES PRIOR TO CALLING THE CALCULATION ROUTINES
    XSWP = 0.0D0
	XSWPLOC = 0.0D0
    DO I=1,NPERD
      DO J=1,NOBS
        XSWP(I,J) = XOBS(I,J)
      END DO
      DO J=1,MPR+NPARPREDONLY
        XSWP(I,J+NOBS) = XPRI(I,J)
      END DO
    END DO
	DEALLOCATE(XOBS)
	DEALLOCATE(XPRI)
    !
    ! LOAD TEMPNAME ARRAY FOR PRINTING MATRICES
    TEMPNAME=' '
    DO N=1,NOBS
      TEMPNAME(N) = OBSNAME(N)
    END DO
    DO N=1,MPR+NPARPREDONLY
      TEMPNAME(N+NOBS) = PRIORNAME(N)
    END DO
    
!crt    CALL CPU_TIME(RTIME)                                    !TIME CHECKS
!crt    WRITE(*,*) '5. ALLOCATING WORKING ARRAYS ',0,RTIME      !TIME CHECKS
    
    !
    ! ALLOCATE THE ARRAYS THAT SUBROUTINE OPRPPR_CALC WILL WORK WITH
	ALLOCATE(XTW(NPERD,MAXOB),CMAT(NPERD,NPERD),AINV(NPERD,NPERD),ZV(NPRED,NPRED), &
	         D(NPERD,NPERD*2),ZCP(NPERD,NPRED),STAT=IERR)
    IF(ALLOCATEERROR(IOUT,IERR,'ALLOCATION: WORKING ARRAYS')) CALL UTL_STOP('(MAIN)')
    !
    ! ESTABLISH BASE CASE - FILL XSWP WITH EXISTING OBS AND PRIOR, SIZE CDMATRIX THAT WILL DO THE WORK
	CONVERGED=.TRUE. ! DEFAULT SO MATRICES PRINT IN OPR_PPR_CALC FOR ALL OPR STATISTICS
    PARV=0.0D0
    PVAR=0.0D0
    PARCORR=0.0
    PRIORWT=0.0D0
    ITER=0
    CALL TYP_NULL(WTMALL)
    NNZ = NUMNONZ(WTM,NDINC+MPR+NPARPREDONLY,NDINC+MPR+NPARPREDONLY)
	NNZF=NNZ
    
!crt    CALL CPU_TIME(RTIME)                                        !TIME CHECKS
!crt    WRITE(*,*) '6. CONSTRUCT/INITIALIZE MATRICES ',0,RTIME      !TIME CHECKS
    
    CALL UTL_CONSTRUCTCDMATRIX(NNZ+(MAXNEW*MAXNEW),MAXOB,MAXOB,WTMALL,'WTMALL')
	CALL OPRPPR_INITWTMALL(WTMALL,WTM,NDINC+MPR+NPARPREDONLY,NNZ,MAXNEW)
	XSWPLOC=XSWP !WORKING ARRAY
	
!crt	CALL CPU_TIME(RTIME)                                        !TIME CHECKS
!crt    WRITE(*,*) '7. FIRST MATRIX UPDATES ',0,RTIME               !TIME CHECKS
    
	CALL OPRPPR_UPDATEMATS(MAXOB,XSWPLOC,XOBN,TOTNEW,WTMALL,ITER,WTN,TNAME,WTM,PARNAM, &
                             OBSNAME,PRIORWT,PARIND,IVC,1,OPRNAME,PRIORNAME,TEMPNAME,NNZF)
    WRITE(IOUT,35)
   	
!crt   	CALL CPU_TIME(RTIME)                                        !TIME CHECKS
!crt    WRITE(*,*) '8. BASE CASE CALCULATION ',0,RTIME              !TIME CHECKS
    
    CALL OPRPPR_CALC(MAXOB,PARNAM,PREDNAME,XSWPLOC,ZPRE,WTMALL,PVAR,PARV,PARCORR,ITER,IVC,CMDI)
    
!crt    CALL CPU_TIME(RTIME)                                        !TIME CHECKS
!crt    WRITE(*,*) '9. DONE CASE CALCULATION ',0,RTIME              !TIME CHECKS
    
    WRITE(IOUT,40)
    !
    ! IF BASECASE=YES EXIT HERE OTHERWISE MAKE THE CALCULATIONS.
      IF(BASECASE.EQ.'YES') THEN
        WRITE(IOUT,42)
      ELSEIF(RUNFLAG.EQ.-1.OR.RUNFLAG.EQ.1) THEN     !OPR: INDIVIDUAL ADDITION OR OMISSION
	    IF(MAXCALCS.GT.0) NUMITER=MAXCALCS
	    IF(MAXCALCS.LE.0) NUMITER=NUMOBS(1)
        DO ITER=1,NUMITER
          IF((NUMOBS(1).GT.100).AND.(MOD(ITER,10).EQ.0)) WRITE(*,90) ITER,NUMOBS(1)    !REPORT PROGRESS
          XSWPLOC=XSWP                                    !WORKING ARRAY
		  ! REINITIALIZING WEIGHTMATRIX NOT REQUIRED: ONLY LAST ENTRY MODIFIED
		  IF(RUNFLAG.EQ.-1) WTMALL=WTM    !RE-ASSIGN OKAY SINCE OMITTING
          CALL OPRPPR_UPDATEMATS(MAXOB,XSWPLOC,XOBN,TOTNEW,WTMALL,ITER,WTN,TNAME,WTM,PARNAM, &
                                 OBSNAME,PRIORWT,PARIND,IVC,1,OPRNAME,PRIORNAME,TEMPNAME,NNZF)
          CALL OPRPPR_CALC(MAXOB,PARNAM,PREDNAME,XSWPLOC,ZPRE,WTMALL,PVAR,PARV,PARCORR,ITER,IVC,CMDI)
        END DO
      ELSEIF (RUNFLAG.EQ.-2.OR.RUNFLAG.EQ.2) THEN    !OPR: GROUP ADDITION OR OMISSION
        DO ITER=1,NUMGPS
		  XSWPLOC=XSWP                                    !WORKING ARRAY
		  IF(RUNFLAG.EQ.-2) WTMALL=WTM    !RE-ASSIGN OKAY SINCE OMITTING
		  IF(RUNFLAG.EQ.2)  CALL OPRPPR_INITWTMALL(WTMALL,WTM,NDINC+MPR+NPARPREDONLY,NNZ,MAXNEW)
		  CALL OPRPPR_UPDATEMATS(MAXOB,XSWPLOC,XOBN,TOTNEW,WTMALL,ITER,WTN,TNAME,WTM,PARNAM, &
                                 OBSNAME,PRIORWT,PARIND,IVC,1,OPRNAME,PRIORNAME,TEMPNAME,NNZF)
          CALL OPRPPR_CALC(MAXOB,PARNAM,PREDNAME,XSWPLOC,ZPRE,WTMALL,PVAR,PARV,PARCORR,ITER,IVC,CMDI)
        END DO
      ELSEIF(RUNFLAG.EQ.3) THEN                      !OPR: NODE-BASED OBSERVATIONS TO BE ADDED
        IF(MAXNODECALCS.GT.0) NUMITER=MAXNODECALCS
	    IF(MAXNODECALCS.LE.0) NUMITER=NUMOBS(1)
	    DO ITER=1,NUMITER
	      XSWPLOC=XSWP                                    !WORKING ARRAY
		  IF((NUMOBS(1).GT.100).AND.(MOD(ITER,10).EQ.0)) WRITE(*,90) ITER,NUMOBS(1)    !REPORT PROGRESS
		  ! REINITIALIZING WEIGHTMATRIX NOT REQUIRED: ONLY LAST ENTRY MODIFIED
          CALL OPRPPR_UPDATEMATS(MAXOB,XSWPLOC,XOBN,TOTNEW,WTMALL,ITER,WTN,TNAME,WTM,PARNAM, &
                                 OBSNAME,PRIORWT,PARIND,IVC,1,OPRNAME,PRIORNAME,TEMPNAME,NNZF)
          CALL OPRPPR_CALC(MAXOB,PARNAM,PREDNAME,XSWPLOC,ZPRE,WTMALL,PVAR,PARV,PARCORR,ITER,IVC,CMDI)
        END DO
      ELSEIF(RUNFLAG.EQ.11) THEN        !PPR: INDIVIDUAL PARAMETER PRIOR
        DO ITER=1,NUMOBS(1)
	      XSWPLOC=XSWP                                    !WORKING ARRAY
          WRITE(IOUT,100)TRIM(ADJUSTL(PARNAM(PARIND(ITER,1))))
          CONVERGED=.FALSE.
          INIT=.TRUE.
          NITER=0
          CALL OPRPPR_GETWTPRIOR(PARNAM,PARV,CMDI,IVC,PRIORWT,PARIND(ITER,1),ITER)
          DO WHILE (.NOT.CONVERGED)
            NITER=NITER+1
            CALL OPRPPR_UPDATEMATS(MAXOB,XSWPLOC,XOBN,TOTNEW,WTMALL,ITER,WTN,TNAME,WTM,PARNAM, &
                                   OBSNAME,PRIORWT,PARIND,IVC,1,OPRNAME,PRIORNAME,TEMPNAME,NNZF)
            CALL OPRPPR_CALC(MAXOB,PARNAM,PREDNAME,XSWPLOC,ZPRE,WTMALL,PVAR,PARV,PARCORR,ITER,IVC,CMDI)
            CALL OPRPPR_GETWTPRIOR(PARNAM,PARV,CMDI,IVC,PRIORWT,PARIND(ITER,1),ITER)
          END DO
          WRITE(IOUT,110) TRIM(ADJUSTL(PARNAM(PARIND(ITER,1)))),NITER
          CALL OPRPPR_CALC(MAXOB,PARNAM,PREDNAME,XSWPLOC,ZPRE,WTMALL,PVAR,PARV,PARCORR,ITER,IVC,CMDI)
        END DO
      ELSEIF(RUNFLAG.EQ.12) THEN           !PPR: GROUPED PARAMETER PRIOR
        FC=.FALSE.
        ITER=1
        DO ICALC=1,NPERD
	      XSWPLOC=XSWP                                    !WORKING ARRAY
          CONVERGED=.FALSE.
          INIT=.TRUE.
          NITER=0
          CALL OPRPPR_GETWTPRIOR(PARNAM,PARV,CMDI,IVC,PRIORWT,ICALC,ITER)
          DO WHILE (.NOT.CONVERGED)
            NITER=NITER+1
            CALL OPRPPR_UPDATEMATS(MAXOB,XSWPLOC,XOBN,TOTNEW,WTMALL,ITER,WTN,TNAME,WTM,PARNAM, &
                                   OBSNAME,PRIORWT,PARIND,IVC,ICALC,OPRNAME,PRIORNAME,TEMPNAME,NNZF)
            CALL OPRPPR_CALC(MAXOB,PARNAM,PREDNAME,XSWPLOC,ZPRE,WTMALL,PVAR,PARV,PARCORR,ITER,IVC,CMDI)
            CALL OPRPPR_GETWTPRIOR(PARNAM,PARV,CMDI,IVC,PRIORWT,ICALC,ITER)
          END DO
          WRITE(IOUT,110) TRIM(ADJUSTL(PARNAM(ICALC))),NITER
        END DO
        FC=.TRUE.
        CONVERGED=.TRUE.
        DO ITER=1,NUMGPS
	      XSWPLOC=XSWP                                    !WORKING ARRAY
          WRITE(IOUT,120)ITER
          !MAKE Z(INV(XTwX)s)Zt CALCULATION WITH ALL MEMBERS OF THIS GROUP USING THE WEIGHTS DETERMINED ABOVE
          CALL OPRPPR_UPDATEMATS(MAXOB,XSWPLOC,XOBN,TOTNEW,WTMALL,ITER,WTN,TNAME,WTM,PARNAM, &
                                 OBSNAME,PRIORWT,PARIND,IVC,ITER,OPRNAME,PRIORNAME,TEMPNAME,NNZF)
          CALL OPRPPR_CALC(MAXOB,PARNAM,PREDNAME,XSWPLOC,ZPRE,WTMALL,PVAR,PARV,PARCORR,ITER,IVC,CMDI)
        END DO
      ELSEIF(RUNFLAG.EQ.13) THEN                     !PPR: USER-DEFINED GROUPED PARAMETER PRIOR
        CALL UTL_STOP('USER-DEFINED GROUPED PARAMETERS (RUNFLAG=13) IS NOT ACTIVATED (MAIN)')
      END IF
      !
      ! REPORT RESULTS
      IF(BASECASE.NE.'YES') CALL OPRPPR_WRITE(PVAR,PARV,IVC,PARNAM,PREDNAME,NTIMES,PARCORR, &
                                              NCOL,NROW,NLAY,OPRNAME,GRPNAME,I_PREDDAT,I_PREDGRP,PGRPNAME)
      !
      ! DEALLOCATE ARRAYS
	  IF(ALLOCATED(AINV))DEALLOCATE(AINV)
	  IF(ALLOCATED(CMAT))DEALLOCATE(CMAT)
      IF(ALLOCATED(CMDI))DEALLOCATE(CMDI)
	  IF(ALLOCATED(D))DEALLOCATE(D)
      IF(ALLOCATED(GRPNAME))DEALLOCATE(GRPNAME)
      IF(ALLOCATED(IPTR))DEALLOCATE(IPTR)
      IF(ALLOCATED(KPER))DEALLOCATE(KPER)
      IF(ALLOCATED(KSTP))DEALLOCATE(KSTP)
      IF(ALLOCATED(LN))DEALLOCATE(LN)
      IF(ALLOCATED(NUMOBS))DEALLOCATE(NUMOBS)
      IF(ALLOCATED(NUMPRED))DEALLOCATE(NUMPRED)
      IF(ALLOCATED(OBSNAM))DEALLOCATE(OBSNAM)
	  IF(ALLOCATED(OMIT_DATA_VAL))DEALLOCATE(OMIT_DATA_VAL)
      IF(ALLOCATED(OPRNAME))DEALLOCATE(OPRNAME)
      IF(ALLOCATED(PARCORR))DEALLOCATE(PARCORR)
      IF(ALLOCATED(PARIND))DEALLOCATE(PARIND)
      IF(ALLOCATED(PARNAM))DEALLOCATE(PARNAM)
      IF(ALLOCATED(PARNAMD))DEALLOCATE(PARNAMD)
      IF(ALLOCATED(PARV))DEALLOCATE(PARV)
      IF(ALLOCATED(PARVAL))DEALLOCATE(PARVAL)
      IF(ALLOCATED(PERTIM))DEALLOCATE(PERTIM)
      IF(ALLOCATED(PGRPNAME))DEALLOCATE(PGRPNAME)
      IF(ALLOCATED(PLOTSYMBOLD))DEALLOCATE(PLOTSYMBOLD)
      IF(ALLOCATED(PNAMTEMP))DEALLOCATE(PNAMTEMP)
      IF(ALLOCATED(PNAMTMP1))DEALLOCATE(PNAMTMP1)
      IF(ALLOCATED(PREDNAM))DEALLOCATE(PREDNAM)
      IF(ALLOCATED(PREDNAME))DEALLOCATE(PREDNAME)
      IF(ALLOCATED(PREDONLYPRIOR_STAT))DEALLOCATE(PREDONLYPRIOR_STAT)
      IF(ALLOCATED(PREDONLYPRIOR_STATFLAG))DEALLOCATE(PREDONLYPRIOR_STATFLAG)
      IF(ALLOCATED(PREDONLYPRIOR_WT))DEALLOCATE(PREDONLYPRIOR_WT)
      IF(ALLOCATED(PRIORNAME))DEALLOCATE(PRIORNAME)
      IF(ALLOCATED(PRIORWT))DEALLOCATE(PRIORWT)
      IF(ALLOCATED(PVAR))DEALLOCATE(PVAR)
      IF(ALLOCATED(TNAME))DEALLOCATE(TNAME)
      IF(ALLOCATED(TOTIM))DEALLOCATE(TOTIM)
      IF(ALLOCATED(USEFLAGP))DEALLOCATE(USEFLAGP)
      IF(ALLOCATED(XOBN))DEALLOCATE(XOBN)
      IF(ALLOCATED(XOBS))DEALLOCATE(XOBS)
      IF(ALLOCATED(XPRI))DEALLOCATE(XPRI)
      IF(ALLOCATED(XSWP))DEALLOCATE(XSWP)
      IF(ALLOCATED(XSWPLOC))DEALLOCATE(XSWPLOC)
	  IF(ALLOCATED(XTW))DEALLOCATE(XTW)
	  IF(ALLOCATED(ZCP))DEALLOCATE(ZCP)
      IF(ALLOCATED(ZPRE))DEALLOCATE(ZPRE)
	  IF(ALLOCATED(ZV))DEALLOCATE(ZV)
	!
      CALL TYP_DEALLOC(WTM)
      CALL TYP_DEALLOC(WTN)
      CALL TYP_DEALLOC(WTMALL)
    	!
    	! REPORT PROGRAM END
      WRITE(IOUT,900) PROGNAM
900   FORMAT(/,1X,38('='),/,1X,'SUCCESSFUL PROGRAM EXECUTION - ',A,/,1X,38('='))
      CALL UTL_STOP('SUCCESSFUL PROGRAM EXECUTION - '//PROGNAM)
999   CALL UTL_STOP('ERROR ENCOUNTERED READING OUTPUT FILE NAME - STOP (MAIN)')
      !
END PROGRAM OPR_PPR
